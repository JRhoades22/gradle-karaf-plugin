
// *****************************************************************************
//
// *****************************************************************************

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

// *****************************************************************************
//
// *****************************************************************************

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    //id 'maven-publish'
    id 'signing'
    id 'com.gradle.plugin-publish' version '0.9.5'
    id 'net.researchgate.release' version '2.4.1'
}

//apply plugin: 'io.codearte.nexus-staging'

// *****************************************************************************
//
// *****************************************************************************

group               = 'com.github.lburgazzoli'
description         = 'A gradle plugin for Karaf'
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

ext {
    gradleVersion     = '3.0'
    gradleScriptDir   = "${rootProject.projectDir}/gradle"
    gradlePluginId    = "com.github.lburgazzoli.karaf"
    gradlePluginTags  = [ 'karaf' ]
    isReleaseVersion  = !version.endsWith("SNAPSHOT")
    isSnapshotVersion = version.endsWith("SNAPSHOT")
    isCI              = Boolean.valueOf("$System.env.CI")
    gitRoot           = "https://github.com/lburgazzoli"
    gitProject        = "https://github.com/lburgazzoli/gradle-karaf-plugin"
    gitURL            = "git@github.com:lburgazzoli/gradle-karaf-plugin"
    groovyVersion     = GroovySystem.version.replaceAll(/\.\d+$/,'')

    versions = [
        slf4j : '1.7.21',
        spock : "1.0-groovy-${groovyVersion}"
    ]

    if (isReleaseVersion) {
        nexusUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    } else {
        nexusUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    if (isCI && isReleaseVersion) {
        nexusUsername = ""
        nexusPassword = ""
    } else if(!project.hasProperty('nexusUsername') && !project.hasProperty('nexusPassword')) {
        nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
        nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
    }
}

repositories {
    mavenCentral()    
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile("org.spockframework:spock-core:$versions.spock") {
        exclude(module: 'groovy-all')
    }
}

project.tasks.withType(org.gradle.api.tasks.compile.JavaCompile) {
    it.sourceCompatibility = project.sourceCompatibility
    it.targetCompatibility = project.targetCompatibility
}

project.tasks.withType(org.gradle.api.tasks.compile.GroovyCompile) {
    it.sourceCompatibility = project.sourceCompatibility
    it.targetCompatibility = project.targetCompatibility
}

jar {
    baseName = "${project.name}"

    manifest {
        attributes['Implementation-Title'   ] = "${group}.${project.name}-${project.version}"
        attributes['Implementation-Version' ] = project.version
        attributes['Implementation-Vendor'  ] = 'Luca Burgazzoli'
    }
}

// *****************************************************************************
// ARTIFACTS
// *****************************************************************************

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
    archives javadocJar
}

signing {
    required { 
        //isReleaseVersion  
        false
    }

    sign configurations.archives
}

// *****************************************************************************
// PUBLISHING
// *****************************************************************************

// Gradle Plugin
pluginBundle {
    website     = project.gitProject
    vcsUrl      = project.gitProject
    description = project.name
    tags        = gradlePluginTags

    plugins {
        karafPlugin {
            id          = gradlePluginId
            displayName = project.description
        }
    }
}

// Maven (dosabled as there is issues with signing)
/* 
publishing {
    repositories {
        maven {
            credentials {
                username project.nexusUsername
                password project.nexusPassword
            }

            url project.nexusUrl
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.name)
                root.appendNode('url', project.gitProject)
                root.appendNode('inceptionYear', '2014')

                def scm = root.appendNode('scm')
                scm.appendNode('url', project.gitProject)
                scm.appendNode('connection', "scm:${project.gitProject}")
                scm.appendNode('developerConnection', , "scm:${project.gitURL}")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developers = root.appendNode('developers')
                def bmuschko = developers.appendNode('developer')
                bmuschko.appendNode('id', 'lburgazzoli')
                bmuschko.appendNode('name', 'Luca Burgazzoli')
            }
        }
    }
}
*/

// *****************************************************************************
//
// *****************************************************************************

afterReleaseBuild.dependsOn publishPlugins, publish

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}
